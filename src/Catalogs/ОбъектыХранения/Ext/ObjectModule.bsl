
&ИзменениеИКонтроль("ОбработкаЗаполнения")
Процедура дсфОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("МодельУчета") Тогда
			МодельУчета = ДанныеЗаполнения.МодельУчета;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(МодельУчета) Тогда
			Если ЗначениеЗаполнено(Родитель) Тогда
				МодельУчета = Родитель.МодельУчета;
			КонецЕсли;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(МодельУчета) Тогда
			МодельУчета = Справочники.МоделиУчета.Основная;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Номенклатура") Тогда
		Номенклатура = ДанныеЗаполнения;
		Если Не ЗначениеЗаполнено(МодельУчета)
			И (ЗначениеЗаполнено(Организация) 
			Или ЗначениеЗаполнено(Контрагент) 
			Или ЗначениеЗаполнено(ХарактеристикаНоменклатуры))
			Тогда
			ОбъектХраненияОсновной = Справочники.Номенклатура.НайтиОбъектХранения(Номенклатура);
			Если ЗначениеЗаполнено(ОбъектХраненияОсновной) Тогда
				ЭтотОбъект.МодельУчета = ОбъектХраненияОсновной.МодельУчета;
			КонецЕсли;
		КонецЕсли;
		Родитель = Справочники.Номенклатура.НайтиОбъектХранения(Номенклатура.Родитель);
		Если Не ЗначениеЗаполнено(МодельУчета) Тогда
			Если ЗначениеЗаполнено(Родитель) Тогда
				МодельУчета = Родитель.МодельУчета;
			КонецЕсли;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(МодельУчета) Тогда
			МодельУчета = Справочники.МоделиУчета.Основная;
		КонецЕсли;
		Если ЭтоГруппа Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Номенклатура, "Код, Наименование"); 
		Иначе
			Если ЭтоНовый() Тогда 
				НовыйЭлемент = Истина;
			КонецЕсли; 
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Номенклатура,
#Удаление			
			"Код, Наименование, Артикул, ЕдиницаИзмерения, Комментарий, Масса, МассаНетто, НаименованиеEn, НаименованиеПолное, Объем, ОбъемНетто, Цена, ЯвляетсяКомплектом, ТорговаяМарка, URLИзображения");
#КонецУдаления
#Вставка
			// дополнительно копируем и номенклатуры значение "дополнительного" артикула "дсфАртикулПолный",
			// добавленного расширением
			"Код, Наименование, Артикул, дсфАртикулПолный, ЕдиницаИзмерения, Комментарий, Масса, МассаНетто, НаименованиеEn, НаименованиеПолное, Объем, ОбъемНетто, Цена, ЯвляетсяКомплектом, ТорговаяМарка, URLИзображения");
#КонецВставки
			Если ЗначениеЗаполнено(Номенклатура.Организация) Тогда
				Организация = Номенклатура.Организация;
			КонецЕсли;	 
			ВидОбъектаХранения = Номенклатура.ВидНоменклатуры;
			Если Не ЗначениеЗаполнено(СостояниеПоУмолчанию) Тогда
				СостояниеПоУмолчанию = Справочники.СостоянияОбъектовХранения.Годен;
			КонецЕсли; 
			Если ЗначениеЗаполнено(Номенклатура.СрокХранения) Тогда 
				СрокХранения = НайтиСоздатьСрокХранения(Номенклатура);
			ИначеЕсли ЗначениеЗаполнено(МодельУчета) И Не ЗначениеЗаполнено(СрокХранения) Тогда 
				Если МодельУчета.УчетПоСрокамГодности Тогда 
					СрокХранения = НайтиСоздатьСрокХранения(Номенклатура);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры
