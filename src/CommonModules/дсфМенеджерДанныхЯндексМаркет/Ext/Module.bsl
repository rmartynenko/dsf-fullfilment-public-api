#Область ПрограммныйИнтерфейс

#Область Справочники

// Процедура - Создать изменить номенклатуру яндекс маркет
//
// Параметры:
//  ДанныеНоменклатуры	 - СписокXdto - 
//  Результат			 - Структура - см. dsf_ОбработкаЗапросовЯндексМаркет.НовыйРезультатОбработкиВходящегоЗапроса
//
Процедура СоздатьОбновитьНоменклатуруЯндексМаркет(ДанныеНоменклатуры, Результат) Экспорт
	
	// обработка

	// формирование Объекта xdto
	Результат["Данные"] = Неопределено;
	
	Для каждого ОписаниеТовара Из ДанныеНоменклатуры Цикл
		
		Ошибка = дсфОбработкаОшибокЯндексМаркет.НоваяОшибкаПриВыполненииЗапроса();
		
		Ошибка.code = 1000; 
		Ошибка.message = "Регистрация товара";
		Ошибка.description = ОписаниеТовара.name;
		
		Результат.Ошибки.Добавить(Ошибка);
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ПоступлениеТоваров

Процедура СоздатьОбновитьЗаявкуНаПриемкуЯндексМаркет(ДанныеЗаявки, Результат) Экспорт
	
	ОписаниеЗаявки = дсфСозданиеОбновлениеДанныхЯндексМаркет.НовоеОписаниеЗаявкиНаПриемку();
	
	ОписаниеЗаявки.Дата = ТекущаяДатаСеанса();
	
	Интервал = дсфОбщегоНазначенияЯндексМаркет.ИнтервалДатИзСтроки(ДанныеЗаявки["interval"]);
	ОписаниеЗаявки.ПериодДоставкиНачало = Интервал["НачалоИнтервала"];
	ОписаниеЗаявки.ПериодДоставкиОкончание = Интервал["ОкончаниеИнтервала"];
	
	ИдентификаторРесурса = дсфОбменДаннымиXDTOЯндексМаркет.ИдентификаторРесурсаИзОбъектаXdto(ДанныеЗаявки["inboundId"]);
	ОписаниеЗаявки.ИдентификаторВнешний = ИдентификаторРесурса["yandexId"];
	ОписаниеЗаявки.Идентификатор = ИдентификаторРесурса["partnerId"];
	
	ОписаниеЗаявки.Комментарий = ДанныеЗаявки["comment"];
	ОписаниеЗаявки.ДанныеЗаявкиВходящие = дсфФормированиеОтветовЯндексМаркет.СодержимоеОтветаВСтрокуXML(ДанныеЗаявки);
	
	ОписаниеЗаявки.ДанныеСкладаЗначенияПолей = 
							дсфОбменДаннымиXDTOЯндексМаркет.ОбъектXDTOВJson(ДанныеЗаявки["logisticPoint"], Истина);

	ОписаниеЗаявки.ДанныеКурьераЗначенияПолей = 
							дсфОбменДаннымиXDTOЯндексМаркет.ОбъектXDTOВJson(ДанныеЗаявки["courier"], Истина);

	ОписаниеЗаявки.ДанныеОтправителяГрузаЗначенияПолей = 
							дсфОбменДаннымиXDTOЯндексМаркет.ОбъектXDTOВJson(ДанныеЗаявки["shipper"], Истина);

	ОписаниеЗаявки.ТипПриемки = дсфОбщегоНазначенияЯндексМаркет.ТипПриемкиПоЧисловомуКоду(ДанныеЗаявки["inboundType"]); 
	
	РезультатОбработкиЗаявки = дсфСозданиеОбновлениеДанныхЯндексМаркет.РезультатОбработкиЗаявкиНаПриемку();
	
	дсфСозданиеОбновлениеДанныхЯндексМаркет.СоздатьОбновитьЗаявкуНаПриемку(ОписаниеЗаявки, РезультатОбработкиЗаявки);
		
	ТипОбъекта = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "ResourceId");
	ДанныеОтвета = ФабрикаXDTO.Создать(ТипОбъекта);
	
	ДанныеОтвета["yandexId"] = РезультатОбработкиЗаявки["ИдентификаторВнешний"];
	Если ЗначениеЗаполнено(РезультатОбработкиЗаявки["Идентификатор"]) Тогда
		ДанныеОтвета["partnerId"] = РезультатОбработкиЗаявки["Идентификатор"];
		
	КонецЕсли;

	Если Не РезультатОбработкиЗаявки.Успех Тогда
		Для каждого ТекОшибка Из РезультатОбработкиЗаявки.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат.Ошибки.Добавить(НоваяОшибка);

		КонецЦикла;
		
	КонецЕсли;	

	// формирование Объекта xdto
	Результат["Данные"] = ДанныеОтвета;
	
КонецПроцедуры

Процедура СоздатьОбновитьРеестрПриемкиЯндексМаркет(ДанныеРеестра, Результат) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОписаниеРеестра = дсфСозданиеОбновлениеДанныхЯндексМаркет.НовоеОписаниеРеестраПриемки();
	
	ОписаниеРеестра.ЗначенияПолей = дсфОбменДаннымиXDTOЯндексМаркет.ОбъектXDTOВJson(ДанныеРеестра, Истина);

	ДатаРеестра = дсфОбщегоНазначенияЯндексМаркет.ДатаИзСтроки(ДанныеРеестра["date"]);
	
	Если ЗначениеЗаполнено(ДатаРеестра) Тогда
		ОписаниеРеестра.Дата = ДатаРеестра;

	Иначе	
		ОписаниеРеестра.Дата = ТекущаяДатаСеанса();

	КонецЕсли;
	
	ИдентификаторРесурса = дсфОбменДаннымиXDTOЯндексМаркет.ИдентификаторРесурсаИзОбъектаXdto(ДанныеРеестра["registryId"]);
	ОписаниеРеестра.ИдентификаторВнешний = ИдентификаторРесурса["yandexId"];
	ОписаниеРеестра.Идентификатор = ИдентификаторРесурса["partnerId"];
	
	ИдентификаторЗаявки = дсфОбменДаннымиXDTOЯндексМаркет.ИдентификаторРесурсаИзОбъектаXdto(ДанныеРеестра["inboundId"]);
	ОписаниеРеестра.ИдентификаторЗаявкиНаПриемкуВнешний = ИдентификаторЗаявки["yandexId"];
	ОписаниеРеестра.ИдентификаторЗаявкиНаПриемку = ИдентификаторЗаявки["partnerId"];

	ОписаниеРеестра.ТипРеестра = дсфОбщегоНазначенияЯндексМаркет.ТипРеестраПоЧисловомуКоду(ДанныеРеестра["registryType"]);
	ОписаниеРеестра.ТипРеестраКод = ДанныеРеестра["registryType"];

	ОписаниеРеестра.Комментарий = ДанныеРеестра["comment"];
	
	// палеты
	Если ДанныеРеестра["pallets"] <> Неопределено Тогда
		ПалетыСписокXdto = ДанныеРеестра["pallets"]["pallet"];
		Для каждого ОписаниеПалеты Из ПалетыСписокXdto Цикл
			ОписаниеПалетыXdto = ОписаниеПалеты["unitInfo"];
			
			ОписаниеСтрокиГрузовогоМеста = 
							дсфСозданиеОбновлениеДанныхЯндексМаркет.НовоеОписаниеСтрокиПланаПоступленияГрузовогоМеста();
			
			ОписаниеСтрокиГрузовогоМеста["ТипМХ"] = дсфОбщегоНазначенияЯндексМаркет.ТипМестаХраненияДляПалетПоУмолчанию();
			ОписаниеСтрокиГрузовогоМеста["Количество"] = 1;
			
			ОписаниеРеестра.ГрузовыеМеста.Добавить(ОписаниеСтрокиГрузовогоМеста);
			
		КонецЦикла;

	КонецЕсли;
	
	// короба
	Если ДанныеРеестра["boxes"] <> Неопределено Тогда
		КоробаСписокXdto = ДанныеРеестра["boxes"]["box"];
		Для каждого ОписаниеКороба Из КоробаСписокXdto Цикл
			ОписаниеКоробаXdto = ОписаниеКороба["unitInfo"];
			
			ОписаниеСтрокиГрузовогоМеста = 
							дсфСозданиеОбновлениеДанныхЯндексМаркет.НовоеОписаниеСтрокиПланаПоступленияГрузовогоМеста();
			
			ОписаниеСтрокиГрузовогоМеста["ТипМХ"] = дсфОбщегоНазначенияЯндексМаркет.ТипМестаХраненияДляКоробовПоУмолчанию();
			ОписаниеСтрокиГрузовогоМеста["Количество"] = 1;
			
			ОписаниеРеестра.ГрузовыеМеста.Добавить(ОписаниеСтрокиГрузовогоМеста);
		
		КонецЦикла;

	КонецЕсли;
	
	// номенклатура
	Если ДанныеРеестра["items"] <> Неопределено Тогда
		ТоварыСписокXdto = ДанныеРеестра["items"]["item"];
		Для каждого ЭлементСпискаТоваров Из ТоварыСписокXdto Цикл
			
			ОписаниеТовара = дсфОбменДаннымиXDTOЯндексМаркет.ОписаниеЭлементаСпискаТоваровВРеестре(ЭлементСпискаТоваров);

			ОписаниеРеестра.Состав.Добавить(ОписаниеТовара);
		
		КонецЦикла;
		
	КонецЕсли;
	
	РезультатОбработкиРеестра = дсфСозданиеОбновлениеДанныхЯндексМаркет.РезультатОбработкиРеестра();

	дсфСозданиеОбновлениеДанныхЯндексМаркет.СоздатьОбновитьРеестрПриемки(ОписаниеРеестра, РезультатОбработкиРеестра);
	
	УстановитьПривилегированныйРежим(Ложь);

	ТипОбъекта = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "ResourceId");
	ДанныеОтвета = ФабрикаXDTO.Создать(ТипОбъекта);
	
	ДанныеОтвета["yandexId"] = РезультатОбработкиРеестра["ИдентификаторВнешний"];
	Если ЗначениеЗаполнено(РезультатОбработкиРеестра["Идентификатор"]) Тогда
		ДанныеОтвета["partnerId"] = РезультатОбработкиРеестра["Идентификатор"];
		
	КонецЕсли;

	Если Не РезультатОбработкиРеестра.Успех Тогда
		Для каждого ТекОшибка Из РезультатОбработкиРеестра.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат.Ошибки.Добавить(НоваяОшибка);

		КонецЦикла;
		
	КонецЕсли;	

	// формирование Объекта xdto
	Результат["Данные"] = ДанныеОтвета;

КонецПроцедуры

Процедура ПодготовитьСведенияОСтатусахЗаявокНаПриемку(ИдентификаторыЗаявокНаПриемку, Результат) Экспорт

	УстановитьПривилегированныйРежим(Истина); 
	
	ЗаявкиНаПриемку = Новый Массив;
	
	Для каждого ОписаниеЗаявки Из ИдентификаторыЗаявокНаПриемку Цикл
		
		ИдентификаторЗаявкиВнешний = ОписаниеЗаявки["yandexId"];
		ИдентификаторЗаявки = ОписаниеЗаявки["partnerId"];
		
		Заявка = 
			РегистрыСведений.дсфИдентификаторыЯндексМаркет.СсылкаНаОбъектПоПубличномуИдентификатору(
														ИдентификаторЗаявкиВнешний,
														"ДокументСсылка.дсфЗаявкаНаПриемку");
	
		Если Заявка = Неопределено Тогда
			ТекстОшибки = СтрШаблон("Заявка на приемку с yandexId %1 не была ранее зарегистрирована.", ИдентификаторЗаявкиВнешний);
			
			Ошибка = дсфОбработкаОшибокЯндексМаркет.ТипДанныхНеНайденОшибка();
			Ошибка.description = ТекстОшибки;

			Результат.Ошибки.Добавить(Ошибка);
		
		Иначе
			ЗаявкиНаПриемку.Добавить(Заявка);
			
		КонецЕсли;
	
	КонецЦикла;
	
	РезультатЗапросаСтатусов = дсфПолучениеСведенийЯндексМаркет.СведенияОСтатусахПриемок(ЗаявкиНаПриемку);
	
	Если РезультатЗапросаСтатусов.Успех Тогда
		
		ТипОбъекта = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "InboundsStatus");
		ДанныеОтвета = ФабрикаXDTO.Создать(ТипОбъекта);
		
		ТипОбъектаЭлементСписка = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "InboundStatus");
		ТипОбъектаИдентификаторПриемки = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "ResourceId");

		Для каждого ОписаниеСтатуса Из РезультатЗапросаСтатусов["Статусы"] Цикл
			ЭлементСпискаСтатусов = ФабрикаXDTO.Создать(ТипОбъектаЭлементСписка);
			
			СведенияОбИдентификаторах = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторПриемки);
			СведенияОбИдентификаторах["yandexId"] = ОписаниеСтатуса["ИдентификаторВнешний"];
			СведенияОбИдентификаторах["partnerId"] = ОписаниеСтатуса["ИдентификаторДокумента"];
			СведенияОбИдентификаторах["fulfillmentId"] = ОписаниеСтатуса["ИдентификаторСклада"];
			
			ЭлементСпискаСтатусов["inboundId"] = СведенияОбИдентификаторах;
			
			СтатусДокумента = ОписаниеСтатуса["Статус"];
			ЭлементСпискаСтатусов["statusCode"] = дсфСтатусыДокументовЯндексМаркет.КодСтатусаПриемкиПоСтатусуДокумента(СтатусДокумента);
			ЭлементСпискаСтатусов["date"] = XMLСтрока(ОписаниеСтатуса["ДатаИзменения"]);

			ДанныеОтвета["inboundStatus"].Добавить(ЭлементСпискаСтатусов);

		КонецЦикла;
	
	Иначе
		Для каждого ТекОшибка Из РезультатЗапросаСтатусов.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат["Ошибки"].Добавить(НоваяОшибка);

		КонецЦикла;

	КонецЕсли;
	
	// формирование Объекта xdto
	Результат["Данные"] = ДанныеОтвета;

КонецПроцедуры

Процедура ПодготовитьСведенияОДеталяхПриемки(ИдентификаторыЗаявкиНаПриемку, Результат) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторЗаявкиВнешний = ИдентификаторыЗаявкиНаПриемку["yandexId"];
	ИдентификаторЗаявки = ИдентификаторыЗаявкиНаПриемку["partnerId"];
		
	Заявка = РегистрыСведений.дсфИдентификаторыЯндексМаркет.СсылкаНаОбъектПоПубличномуИдентификатору(
																ИдентификаторЗаявкиВнешний,
																"ДокументСсылка.дсфЗаявкаНаПриемку");
	
	Если Заявка = Неопределено Тогда
		ТекстОшибки = СтрШаблон("Заявка на приемку с yandexId %1 не была ранее зарегистрирована.", ИдентификаторЗаявкиВнешний);
		
		Ошибка = дсфОбработкаОшибокЯндексМаркет.ТипДанныхНеНайденОшибка();
		Ошибка.description = ТекстОшибки;

		Результат.Ошибки.Добавить(Ошибка);
		
		Возврат;
		
	КонецЕсли;
	
	РезультатЗапросаДеталейПриемки = дсфПолучениеСведенийЯндексМаркет.ДеталиПриемкиПоЗаявке(Заявка);
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("inbound", Неопределено);
	ДанныеОтвета.Вставить("registries", Неопределено);
	
	Если РезультатЗапросаДеталейПриемки.Успех Тогда
		
		// заявка
		ТипОбъектаЗаявка = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "Inbound");
		ТипОбъектаИдентификаторУниверсальный = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "ResourceId");
		
		ДанныеЗаявки = ФабрикаXDTO.Создать(ТипОбъектаЗаявка);
			
		СведенияОбИдентификаторахЗаявки = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторУниверсальный);
		СведенияОбИдентификаторахЗаявки["yandexId"] = РезультатЗапросаДеталейПриемки["ИдентификаторЗаявкиВнешний"];
		СведенияОбИдентификаторахЗаявки["partnerId"] = РезультатЗапросаДеталейПриемки["ИдентификаторЗаявки"];
		СведенияОбИдентификаторахЗаявки["fulfillmentId"] = РезультатЗапросаДеталейПриемки["ИдентификаторСклада"];

		ДанныеЗаявки["inboundId"] = СведенияОбИдентификаторахЗаявки;
		
		ДанныеЗаявки["inboundType"] = 
				дсфОбщегоНазначенияЯндексМаркет.КодТипаПриемкиПоЗначениюПеречисления(РезультатЗапросаДеталейПриемки["ТипПриемки"]);
				
		ДанныеЗаявки["interval"] = РезультатЗапросаДеталейПриемки["ПериодДоставкиСтрокой"];
		
		ДанныеОтвета["inbound"] = ДанныеЗаявки;
		
		// реестр приемки - фактический, на момент запроса
		ТипОбъектаСписокРеестров = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "Registries");
		ТипОбъектаРеестр = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "Registry");
		
		СписокРеестров = ФабрикаXDTO.Создать(ТипОбъектаСписокРеестров);
		РеестрПриемки = ФабрикаXDTO.Создать(ТипОбъектаРеестр);
		
		СведенияОбИдентификаторахРеестра = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторУниверсальный);
		СведенияОбИдентификаторахРеестра["yandexId"] = РезультатЗапросаДеталейПриемки["ИдентификаторРеестраВнешний"];
		СведенияОбИдентификаторахРеестра["partnerId"] = РезультатЗапросаДеталейПриемки["ИдентификаторРеестра"];

		РеестрПриемки["registryId"] = СведенияОбИдентификаторахРеестра;
		РеестрПриемки["inboundId"] = СведенияОбИдентификаторахЗаявки;
					
		РеестрПриемки["registryType"] =
							дсфОбщегоНазначенияЯндексМаркет.КодТипаРеестраПоЗначениюПеречисления(РезультатЗапросаДеталейПриемки["ТипРеестра"]);
		РеестрПриемки["date"] = XMLСтрока(РезультатЗапросаДеталейПриемки["ДатаРеестра"]);
		РеестрПриемки["comment"] = РезультатЗапросаДеталейПриемки["КомментарийКРеестру"];

		// принятые товары
		ТипОбъектаСписокТоваров = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "RegistryItems");
		СписокТоваров = ФабрикаXDTO.Создать(ТипОбъектаСписокТоваров);
		
		ТипОбъектаСтрокаРеестра = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "RegistryItem");
		ТипОбъектаДанныеОТоваре = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "UnitInfo");
		ТипОбъектаСписокСКоличеством = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "UnitCounts");
		ТипОбъектаОписаниеКоличества = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "UnitCount");
		ТипОбъектаДополнительныеИд = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "CompositeId");
		ТипОбъектаИдентификаторыЕдиниц = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "PartialIds");
		ТипОбъектаИдентификаторЕдиницы = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "PartialId");
		
		Для каждого ДанныеОПринятомТоваре Из РезультатЗапросаДеталейПриемки["ПринятыеТовары"] Цикл
			СтрокаРеестра = ФабрикаXDTO.Создать(ТипОбъектаСтрокаРеестра);
			
			СтрокаРеестра["name"] = ДанныеОПринятомТоваре["Наименование"];
			
			ДанныеОТоваре = ФабрикаXDTO.Создать(ТипОбъектаДанныеОТоваре);
			
			СписокСКоличеством = ФабрикаXDTO.Создать(ТипОбъектаСписокСКоличеством);

			Для каждого ЭлементОписанияКоличества Из ДанныеОПринятомТоваре["СведенияОКоличестве"] Цикл
				ОписаниеКоличества = ФабрикаXDTO.Создать(ТипОбъектаОписаниеКоличества);
				
				Если ТипЗнч(ЭлементОписанияКоличества.Ключ) = Тип("ПеречислениеСсылка.дсфСтатусыЕдиницТовараЯндексМаркет") Тогда
					ОписаниеКоличества["countType"] = XMLСтрока(ЭлементОписанияКоличества.Ключ);
	
				КонецЕсли;
	
				ОписаниеКоличества["quantity"] = XMLСтрока(ЭлементОписанияКоличества.Значение);
				
				СписокСКоличеством["count"].Добавить(ОписаниеКоличества);

			КонецЦикла;
			
			ДанныеОТоваре["counts"] = СписокСКоличеством;
			
			// идентификаторы номенклатуры
			ИдентификаторыНоменклатуры = ФабрикаXDTO.Создать(ТипОбъектаДополнительныеИд);
			СписокИдентификаторовЕдиниц = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторыЕдиниц);
			Для каждого ЭлементОписанияИдентификаторов Из ДанныеОПринятомТоваре["Идентификаторы"] Цикл
				ОписаниеИдентификатора = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторЕдиницы);
				
				Если ТипЗнч(ЭлементОписанияИдентификаторов.Ключ) = Тип("ПеречислениеСсылка.дсфТипыИдентификаторовЯндексМаркет") Тогда
					ОписаниеИдентификатора["idType"] = XMLСтрока(ЭлементОписанияИдентификаторов.Ключ);
	
				КонецЕсли;
	
				ОписаниеИдентификатора["value"] = XMLСтрока(ЭлементОписанияИдентификаторов.Значение);
				
				СписокИдентификаторовЕдиниц["partialId"].Добавить(ОписаниеИдентификатора);

			КонецЦикла;
			
			ИдентификаторыНоменклатуры["partialIds"] = СписокИдентификаторовЕдиниц;

			ДанныеОТоваре["compositeId"] = ИдентификаторыНоменклатуры;
			
			//
			СтрокаРеестра["unitInfo"] = ДанныеОТоваре;
			
			//
			СписокТоваров["item"].Добавить(СтрокаРеестра);
			
		КонецЦикла;
					
		РеестрПриемки["items"] = СписокТоваров;
		
		СписокРеестров["registry"].Добавить(РеестрПриемки);
		ДанныеОтвета["registries"] = СписокРеестров;
		
	Иначе
		Для каждого ТекОшибка Из РезультатЗапросаДеталейПриемки.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат["Ошибки"].Добавить(НоваяОшибка);

		КонецЦикла;

	КонецЕсли;
	
	// ответ структурой, т.к. надо возвращать несколько объектов
	Результат["Данные"] = ДанныеОтвета;

КонецПроцедуры

Процедура ПодготовитьИсториюСтатусовЗаявкиНаПриемку(ИдентификаторЗаявкиНаПриемку, Результат) Экспорт

	УстановитьПривилегированныйРежим(Истина); 
	
	ЗаявкиНаПриемку = Новый Массив;
	
	ИдентификаторЗаявкиВнешний = ИдентификаторЗаявкиНаПриемку["yandexId"];
	ИдентификаторЗаявки = ИдентификаторЗаявкиНаПриемку["partnerId"];
		
	Заявка = РегистрыСведений.дсфИдентификаторыЯндексМаркет.СсылкаНаОбъектПоПубличномуИдентификатору(
																				ИдентификаторЗаявкиВнешний,
																				"ДокументСсылка.дсфЗаявкаНаПриемку");
	
	Если Заявка = Неопределено Тогда
		ТекстОшибки = СтрШаблон("Заявка на приемку с yandexId %1 не была ранее зарегистрирована.", ИдентификаторЗаявкиВнешний);
			
		Ошибка = дсфОбработкаОшибокЯндексМаркет.ТипДанныхНеНайденОшибка();
		Ошибка.description = ТекстОшибки;

		Результат.Ошибки.Добавить(Ошибка);
		
	Иначе
		ЗаявкиНаПриемку.Добавить(Заявка);
			
	КонецЕсли;
	
	РезультатЗапросаИсторииСтатусов = дсфПолучениеСведенийЯндексМаркет.ИсторияСтатусовПриемок(ЗаявкиНаПриемку);
	
	Если РезультатЗапросаИсторииСтатусов.Успех Тогда
		
		ДанныеПриемки = РезультатЗапросаИсторииСтатусов["ИсторияСтатусовПриемок"][0];
		
		ТипОбъекта = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "InboundStatusHistories");
		ДанныеОтвета = ФабрикаXDTO.Создать(ТипОбъекта);
		
		ТипОбъектаЭлементСпискаПриемок = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "InboundStatusHistory");
		
		ТипОбъектаИдентификаторПриемки = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "ResourceId");
		ТипОбъектаИсторияСтатусов = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "History");
		ТипОбъектаСтатусПриемки = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "InboundStatus");
		
		//
		ЭлементСпискаПриемок = ФабрикаXDTO.Создать(ТипОбъектаЭлементСпискаПриемок);
		ИсторияСтатусов = ФабрикаXDTO.Создать(ТипОбъектаИсторияСтатусов);

		СведенияОбИдентификаторах = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторПриемки);
		СведенияОбИдентификаторах["yandexId"] = ДанныеПриемки["ИдентификаторВнешний"];
		СведенияОбИдентификаторах["partnerId"] = ДанныеПриемки["ИдентификаторДокумента"];
		СведенияОбИдентификаторах["fulfillmentId"] = ДанныеПриемки["ИдентификаторСклада"];
		
		ЭлементСпискаПриемок["inboundId"] = СведенияОбИдентификаторах;
							
		Для каждого ОписаниеСтатуса Из ДанныеПриемки["Статусы"] Цикл
			
			ЭлементСпискаСтатусов = ФабрикаXDTO.Создать(ТипОбъектаСтатусПриемки);
			
			СведенияОбИдентификаторах = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторПриемки);
			СведенияОбИдентификаторах["yandexId"] = ОписаниеСтатуса["ИдентификаторВнешний"];
			СведенияОбИдентификаторах["partnerId"] = ОписаниеСтатуса["ИдентификаторДокумента"];
			СведенияОбИдентификаторах["fulfillmentId"] = ОписаниеСтатуса["ИдентификаторСклада"];
			
			ЭлементСпискаСтатусов["inboundId"] = СведенияОбИдентификаторах;

			СведенияОСтатусе = ФабрикаXDTO.Создать(ТипОбъектаСтатусПриемки);
			
			СтатусДокумента = ОписаниеСтатуса["Статус"];
			ЭлементСпискаСтатусов["statusCode"] = дсфСтатусыДокументовЯндексМаркет.КодСтатусаПриемкиПоСтатусуДокумента(СтатусДокумента);
			ЭлементСпискаСтатусов["date"] = XMLСтрока(ОписаниеСтатуса["ДатаИзменения"]);
			
			ИсторияСтатусов["inboundStatus"].Добавить(ЭлементСпискаСтатусов);
			
		КонецЦикла;

		ЭлементСпискаПриемок["history"] = ИсторияСтатусов;

		ДанныеОтвета["InboundStatusHistory"].Добавить(ЭлементСпискаПриемок);
	
	Иначе
		Для каждого ТекОшибка Из РезультатЗапросаИсторииСтатусов.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат["Ошибки"].Добавить(НоваяОшибка);

		КонецЦикла;

	КонецЕсли;
	
	// формирование Объекта xdto
	Результат["Данные"] = ДанныеОтвета;

КонецПроцедуры

#КонецОбласти

#Область Отгрузка

Процедура СоздатьОбновитьЗаявкуНаОтгрузкуЯндексМаркет(ДанныеЗаявки, Результат) Экспорт
	
	ОписаниеЗаявки = дсфСозданиеОбновлениеДанныхЯндексМаркет.НовоеОписаниеЗаявкиНаОтгрузку();
	
	ОписаниеЗаявки.Дата = ТекущаяДатаСеанса();
	
	Интервал = дсфОбщегоНазначенияЯндексМаркет.ИнтервалДатИзСтроки(ДанныеЗаявки["interval"]);
	ОписаниеЗаявки.ПериодОтправкиНачало = Интервал["НачалоИнтервала"];
	ОписаниеЗаявки.ПериодОтправкиОкончание = Интервал["ОкончаниеИнтервала"];
	
	ИдентификаторРесурса = дсфОбменДаннымиXDTOЯндексМаркет.ИдентификаторРесурсаИзОбъектаXdto(ДанныеЗаявки["outboundId"]);
	ОписаниеЗаявки.ИдентификаторВнешний = ИдентификаторРесурса["yandexId"];
	ОписаниеЗаявки.Идентификатор = ИдентификаторРесурса["partnerId"];
	
	ОписаниеЗаявки.Комментарий = ДанныеЗаявки["comment"];
	ОписаниеЗаявки.ДанныеЗаявкиВходящие = дсфФормированиеОтветовЯндексМаркет.СодержимоеОтветаВСтрокуXML(ДанныеЗаявки);
	
	ОписаниеЗаявки.ДанныеСкладаЗначенияПолей = 
							дсфОбменДаннымиXDTOЯндексМаркет.ОбъектXDTOВJson(ДанныеЗаявки["logisticPoint"], Истина);

	ОписаниеЗаявки.ДанныеКурьераЗначенияПолей = 
							дсфОбменДаннымиXDTOЯндексМаркет.ОбъектXDTOВJson(ДанныеЗаявки["courier"], Истина);

	ОписаниеЗаявки.ТипОтправки = Перечисления.дсфТипыОтправкиЯндексМаркет.СПартнерскогоСклада;
		
	РезультатОбработкиЗаявки = дсфСозданиеОбновлениеДанныхЯндексМаркет.РезультатОбработкиЗаявкиНаОтгрузку();
	
	дсфСозданиеОбновлениеДанныхЯндексМаркет.СоздатьОбновитьЗаявкуНаОтгрузку(ОписаниеЗаявки, РезультатОбработкиЗаявки);
		
	ТипОбъекта = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "ResourceId");
	ДанныеОтвета = ФабрикаXDTO.Создать(ТипОбъекта);
	
	ДанныеОтвета["yandexId"] = РезультатОбработкиЗаявки["ИдентификаторВнешний"];
	Если ЗначениеЗаполнено(РезультатОбработкиЗаявки["Идентификатор"]) Тогда
		ДанныеОтвета["partnerId"] = РезультатОбработкиЗаявки["Идентификатор"];
		
	КонецЕсли;

	Если Не РезультатОбработкиЗаявки.Успех Тогда
		Для каждого ТекОшибка Из РезультатОбработкиЗаявки.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат.Ошибки.Добавить(НоваяОшибка);

		КонецЦикла;
		
	КонецЕсли;
	
	Результат["Данные"] = ДанныеОтвета;

КонецПроцедуры

Процедура СоздатьОбновитьРеестрОтгрузкиЯндексМаркет(ДанныеРеестра, Результат) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОписаниеРеестра = дсфСозданиеОбновлениеДанныхЯндексМаркет.НовоеОписаниеРеестраОтгрузки();
	
	ОписаниеРеестра.ЗначенияПолей = дсфОбменДаннымиXDTOЯндексМаркет.ОбъектXDTOВJson(ДанныеРеестра, Истина);

	ДатаРеестра = дсфОбщегоНазначенияЯндексМаркет.ДатаИзСтроки(ДанныеРеестра["date"]);
	
	Если ЗначениеЗаполнено(ДатаРеестра) Тогда
		ОписаниеРеестра.Дата = ДатаРеестра;

	Иначе	
		ОписаниеРеестра.Дата = ТекущаяДатаСеанса();

	КонецЕсли;
	
	ИдентификаторРесурса = дсфОбменДаннымиXDTOЯндексМаркет.ИдентификаторРесурсаИзОбъектаXdto(ДанныеРеестра["registryId"]);
	ОписаниеРеестра.ИдентификаторВнешний = ИдентификаторРесурса["yandexId"];
	ОписаниеРеестра.Идентификатор = ИдентификаторРесурса["partnerId"];
	
	ИдентификаторЗаявки = дсфОбменДаннымиXDTOЯндексМаркет.ИдентификаторРесурсаИзОбъектаXdto(ДанныеРеестра["outboundId"]);
	ОписаниеРеестра.ИдентификаторЗаявкиНаОтгрузкуВнешний = ИдентификаторЗаявки["yandexId"];
	ОписаниеРеестра.ИдентификаторЗаявкиНаОтгрузку = ИдентификаторЗаявки["partnerId"];

	ОписаниеРеестра.ТипРеестра = дсфОбщегоНазначенияЯндексМаркет.ТипРеестраПоЧисловомуКоду(ДанныеРеестра["registryType"]);
	ОписаниеРеестра.ТипРеестраКод = ДанныеРеестра["registryType"];

	ОписаниеРеестра.Комментарий = ДанныеРеестра["comment"];
	
	// палеты
	Если ДанныеРеестра["pallets"] <> Неопределено Тогда
		ПалетыСписокXdto = ДанныеРеестра["pallets"]["pallet"];
		Для каждого ОписаниеПалеты Из ПалетыСписокXdto Цикл
			ОписаниеПалетыXdto = ОписаниеПалеты["unitInfo"];
			
			ОписаниеСтрокиГрузовогоМеста = 
							дсфСозданиеОбновлениеДанныхЯндексМаркет.НовоеОписаниеСтрокиПланаОтгрузкиГрузовогоМеста();
			
			ОписаниеСтрокиГрузовогоМеста["ТипМХ"] = дсфОбщегоНазначенияЯндексМаркет.ТипМестаХраненияДляПалетПоУмолчанию();
			ОписаниеСтрокиГрузовогоМеста["Количество"] = 1;
			
			ОписаниеРеестра.ГрузовыеМеста.Добавить(ОписаниеСтрокиГрузовогоМеста);
			
		КонецЦикла;
	
	КонецЕсли;
		
	// короба
	Если ДанныеРеестра["boxes"] <> Неопределено Тогда
		КоробаСписокXdto = ДанныеРеестра["boxes"]["box"];
		Для каждого ОписаниеКороба Из КоробаСписокXdto Цикл
			ОписаниеКоробаXdto = ОписаниеКороба["unitInfo"];
			
			ОписаниеСтрокиГрузовогоМеста = 
							дсфСозданиеОбновлениеДанныхЯндексМаркет.НовоеОписаниеСтрокиПланаОтгрузкиГрузовогоМеста();
			
			ОписаниеСтрокиГрузовогоМеста["ТипМХ"] = дсфОбщегоНазначенияЯндексМаркет.ТипМестаХраненияДляКоробовПоУмолчанию();
			ОписаниеСтрокиГрузовогоМеста["Количество"] = 1;
			
			ОписаниеРеестра.ГрузовыеМеста.Добавить(ОписаниеСтрокиГрузовогоМеста);
		
		КонецЦикла;

	КонецЕсли;

	// номенклатура
	Если ДанныеРеестра["items"] <> Неопределено Тогда
		ТоварыСписокXdto = ДанныеРеестра["items"]["item"];
		Для каждого ЭлементСпискаТоваров Из ТоварыСписокXdto Цикл
			
			ОписаниеТовара = дсфОбменДаннымиXDTOЯндексМаркет.ОписаниеЭлементаСпискаТоваровВРеестре(ЭлементСпискаТоваров);

			ОписаниеРеестра.Состав.Добавить(ОписаниеТовара);
		
		КонецЦикла;

	КонецЕсли;

	// поклажедатель и поставщик
	ОписаниеРеестра.Организация = дсфОбщегоНазначенияЯндексМаркет.ПоклажедательПоУмолчанию();
	ОписаниеРеестра.Контрагент = ОписаниеРеестра.Организация;

	
	РезультатОбработкиРеестра = дсфСозданиеОбновлениеДанныхЯндексМаркет.РезультатОбработкиРеестра();

	дсфСозданиеОбновлениеДанныхЯндексМаркет.СоздатьОбновитьРеестрОтгрузки(ОписаниеРеестра, РезультатОбработкиРеестра);
	
	УстановитьПривилегированныйРежим(Ложь);

	ТипОбъекта = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "ResourceId");
	ДанныеОтвета = ФабрикаXDTO.Создать(ТипОбъекта);
	
	ДанныеОтвета["yandexId"] = РезультатОбработкиРеестра["ИдентификаторВнешний"];
	Если ЗначениеЗаполнено(РезультатОбработкиРеестра["Идентификатор"]) Тогда
		ДанныеОтвета["partnerId"] = РезультатОбработкиРеестра["Идентификатор"];
		
	КонецЕсли;

	Если Не РезультатОбработкиРеестра.Успех Тогда
		Для каждого ТекОшибка Из РезультатОбработкиРеестра.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат.Ошибки.Добавить(НоваяОшибка);

		КонецЦикла;
		
	КонецЕсли;

	// формирование Объекта xdto
	Результат["Данные"] = ДанныеОтвета;

КонецПроцедуры

// для api v1
Процедура ПодготовитьСведенияОСтатусахЗаявокНаОтгрузку(ИдентификаторыЗаявокНаОтгрузку, Результат) Экспорт

	УстановитьПривилегированныйРежим(Истина); 
	
	ЗаявкиНаОтгрузку = Новый Массив;
	
	Для каждого ОписаниеЗаявки Из ИдентификаторыЗаявокНаОтгрузку Цикл
		
		ИдентификаторЗаявкиВнешний = ОписаниеЗаявки["yandexId"];
		ИдентификаторЗаявки = ОписаниеЗаявки["partnerId"];
		
		Заявка = РегистрыСведений.дсфИдентификаторыЯндексМаркет.СсылкаНаОбъектПоПубличномуИдентификатору(
																ИдентификаторЗаявкиВнешний,
																"ДокументСсылка.дсфЗаявкаНаОтгрузку");
	
		Если Заявка = Неопределено Тогда
			ТекстОшибки = СтрШаблон("Заявка на отправку с yandexId %1 не была ранее зарегистрирована.", ИдентификаторЗаявкиВнешний);
			
			Ошибка = дсфОбработкаОшибокЯндексМаркет.ТипДанныхНеНайденОшибка();
			Ошибка.description = ТекстОшибки;

			Результат.Ошибки.Добавить(Ошибка);
		
		Иначе
			ЗаявкиНаОтгрузку.Добавить(Заявка);
			
		КонецЕсли;
	
	КонецЦикла;
	
	РезультатЗапросаСтатусов = дсфПолучениеСведенийЯндексМаркет.СведенияОСтатусахОтгрузок(ЗаявкиНаОтгрузку);
	
	Если РезультатЗапросаСтатусов.Успех Тогда
		
		ТипОбъекта = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "OutboundStatuses");
		ДанныеОтвета = ФабрикаXDTO.Создать(ТипОбъекта);
		
		ТипОбъектаЭлементСписка = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "OutboundStatus");
		ТипОбъектаИдентификаторПриемки = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "ResourceId");
		
		ТипОбъектаСтатус = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "Status");

		Для каждого ОписаниеСтатуса Из РезультатЗапросаСтатусов["Статусы"] Цикл
			ЭлементСпискаСтатусов = ФабрикаXDTO.Создать(ТипОбъектаЭлементСписка);
			
			СведенияОбИдентификаторах = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторПриемки);
			СведенияОбИдентификаторах["yandexId"] = ОписаниеСтатуса["ИдентификаторВнешний"];
			СведенияОбИдентификаторах["partnerId"] = ОписаниеСтатуса["ИдентификаторДокумента"];
			СведенияОбИдентификаторах["fulfillmentId"] = ОписаниеСтатуса["ИдентификаторСклада"];
			
			ЭлементСпискаСтатусов["outboundId"] = СведенияОбИдентификаторах;
			
			СведенияОСтатусе = ФабрикаXDTO.Создать(ТипОбъектаСтатус);

			
			СтатусДокумента = ОписаниеСтатуса["Статус"];
			СведенияОСтатусе["statusCode"] = дсфСтатусыДокументовЯндексМаркет.КодСтатусаОтгрузкиПоСтатусуДокумента(СтатусДокумента);
			СведенияОСтатусе["setDate"] = XMLСтрока(ОписаниеСтатуса["ДатаИзменения"]);
			
			ЭлементСпискаСтатусов["status"] = СведенияОСтатусе;
			
			ДанныеОтвета["outboundStatus"].Добавить(ЭлементСпискаСтатусов);

		КонецЦикла;
	
	Иначе
		Для каждого ТекОшибка Из РезультатЗапросаСтатусов.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат["Ошибки"].Добавить(НоваяОшибка);

		КонецЦикла;

	КонецЕсли;
	
	// формирование Объекта xdto
	Результат["Данные"] = ДанныеОтвета;

КонецПроцедуры

// использовать для api v2
Процедура ПодготовитьСведенияОСтатусахЗаявокНаОтгрузку_v2(ИдентификаторыЗаявокНаОтгрузку, Результат) Экспорт

	УстановитьПривилегированныйРежим(Истина); 
	
	ЗаявкиНаОтгрузку = Новый Массив;
	
	Для каждого ОписаниеЗаявки Из ИдентификаторыЗаявокНаОтгрузку Цикл
		
		ИдентификаторЗаявкиВнешний = ОписаниеЗаявки["yandexId"];
		ИдентификаторЗаявки = ОписаниеЗаявки["partnerId"];
		
		Заявка = РегистрыСведений.дсфИдентификаторыЯндексМаркет.СсылкаНаОбъектПоПубличномуИдентификатору(
																ИдентификаторЗаявкиВнешний,
																"ДокументСсылка.дсфЗаявкаНаОтгрузку");
	
		Если Заявка = Неопределено Тогда
			ТекстОшибки = СтрШаблон("Заявка на отправку с yandexId %1 не была ранее зарегистрирована.", ИдентификаторЗаявкиВнешний);
			
			Ошибка = дсфОбработкаОшибокЯндексМаркет.ТипДанныхНеНайденОшибка();
			Ошибка.description = ТекстОшибки;

			Результат.Ошибки.Добавить(Ошибка);
		
		Иначе
			ЗаявкиНаОтгрузку.Добавить(Заявка);
			
		КонецЕсли;
	
	КонецЦикла;
	
	РезультатЗапросаСтатусов = дсфПолучениеСведенийЯндексМаркет.СведенияОСтатусахОтгрузок(ЗаявкиНаОтгрузку);
	
	Если РезультатЗапросаСтатусов.Успех Тогда
		
		ТипОбъекта = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "OutboundsStatus");
		ДанныеОтвета = ФабрикаXDTO.Создать(ТипОбъекта);
		
		ТипОбъектаЭлементСписка = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "OutboundStatus");
		ТипОбъектаИдентификаторПриемки = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "ResourceId");
		
		ТипОбъектаСтатус = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "Status");

		Для каждого ОписаниеСтатуса Из РезультатЗапросаСтатусов["Статусы"] Цикл
			ЭлементСпискаСтатусов = ФабрикаXDTO.Создать(ТипОбъектаЭлементСписка);
			
			СведенияОбИдентификаторах = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторПриемки);
			СведенияОбИдентификаторах["yandexId"] = ОписаниеСтатуса["ИдентификаторВнешний"];
			СведенияОбИдентификаторах["partnerId"] = ОписаниеСтатуса["ИдентификаторДокумента"];
			СведенияОбИдентификаторах["fulfillmentId"] = ОписаниеСтатуса["ИдентификаторСклада"];
			
			ЭлементСпискаСтатусов["outboundId"] = СведенияОбИдентификаторах;
			
			СведенияОСтатусе = ФабрикаXDTO.Создать(ТипОбъектаСтатус);

			
			СтатусДокумента = ОписаниеСтатуса["Статус"];
			СведенияОСтатусе["statusCode"] = дсфСтатусыДокументовЯндексМаркет.КодСтатусаОтгрузкиПоСтатусуДокумента(СтатусДокумента);
			СведенияОСтатусе["date"] = XMLСтрока(ОписаниеСтатуса["ДатаИзменения"]);
			
			ЭлементСпискаСтатусов["status"] = СведенияОСтатусе;
			
			ДанныеОтвета["outboundStatus"].Добавить(ЭлементСпискаСтатусов);

		КонецЦикла;
	
	Иначе
		Для каждого ТекОшибка Из РезультатЗапросаСтатусов.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат["Ошибки"].Добавить(НоваяОшибка);

		КонецЦикла;

	КонецЕсли;
	
	// формирование Объекта xdto
	Результат["Данные"] = ДанныеОтвета;

КонецПроцедуры

// для api v1
Процедура ПодготовитьИсториюСтатусовЗаявокНаОтгрузку(ИдентификаторыЗаявокНаОтгрузку, Результат) Экспорт
	
	УстановитьПривилегированныйРежим(Истина); 
	
	ЗаявкиНаОтгрузку = Новый Массив;
	
	Для каждого ОписаниеЗаявки Из ИдентификаторыЗаявокНаОтгрузку Цикл
		ИдентификаторЗаявкиВнешний = ОписаниеЗаявки["yandexId"];
		ИдентификаторЗаявки = ОписаниеЗаявки["partnerId"];
		
		Заявка = РегистрыСведений.дсфИдентификаторыЯндексМаркет.СсылкаНаОбъектПоПубличномуИдентификатору(
																			ИдентификаторЗаявкиВнешний,
																			"ДокументСсылка.дсфЗаявкаНаОтгрузку");
	
		Если Заявка = Неопределено Тогда
			ТекстОшибки = СтрШаблон("Заявка на отгрузку с yandexId %1 не была ранее зарегистрирована.",
									ИдентификаторЗаявкиВнешний);
				
			Ошибка = дсфОбработкаОшибокЯндексМаркет.ТипДанныхНеНайденОшибка();
			Ошибка.description = ТекстОшибки;

			Результат.Ошибки.Добавить(Ошибка);
			
		Иначе
			ЗаявкиНаОтгрузку.Добавить(Заявка);
				
		КонецЕсли;
	
	КонецЦикла;

	РезультатЗапросаИсторииСтатусов = дсфПолучениеСведенийЯндексМаркет.ИсторияСтатусовОтгрузок(ЗаявкиНаОтгрузку);
	
	Если РезультатЗапросаИсторииСтатусов.Успех Тогда
		
		ТипОбъекта = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "OutboundStatusHistories");
		
		ТипОбъектаЭлементСпискаОтгрузок = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "OutboundStatusHistory");
		
		ТипОбъектаИдентификаторОтгрузки = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "ResourceId");
		ТипОбъектаИсторияСтатусов = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "History");
		
		ТипОбъектаСтатусОтгрузки = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "Status");

		ДанныеОтвета = ФабрикаXDTO.Создать(ТипОбъекта);

		Для каждого ДанныеПриемки Из РезультатЗапросаИсторииСтатусов["ИсторияСтатусовОтгрузок"] Цикл
			
			ЭлементСпискаОтрузок = ФабрикаXDTO.Создать(ТипОбъектаЭлементСпискаОтгрузок);
			ИсторияСтатусов = ФабрикаXDTO.Создать(ТипОбъектаИсторияСтатусов);

			СведенияОбИдентификаторах = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторОтгрузки);
			СведенияОбИдентификаторах["yandexId"] = ДанныеПриемки["ИдентификаторВнешний"];
			СведенияОбИдентификаторах["partnerId"] = ДанныеПриемки["ИдентификаторДокумента"];
			СведенияОбИдентификаторах["fulfillmentId"] = ДанныеПриемки["ИдентификаторСклада"];
			
			ЭлементСпискаОтрузок["outboundId"] = СведенияОбИдентификаторах;
								
			Для каждого ОписаниеСтатуса Из ДанныеПриемки["Статусы"] Цикл
				
				ЭлементСпискаСтатусов = ФабрикаXDTO.Создать(ТипОбъектаСтатусОтгрузки);
							
				СтатусДокумента = ОписаниеСтатуса["Статус"];
				ЭлементСпискаСтатусов["statusCode"] = дсфСтатусыДокументовЯндексМаркет.КодСтатусаОтгрузкиПоСтатусуДокумента(СтатусДокумента);
				ЭлементСпискаСтатусов["setDate"] = XMLСтрока(ОписаниеСтатуса["ДатаИзменения"]);
				
				ИсторияСтатусов["outboundStatus"].Добавить(ЭлементСпискаСтатусов);
				
			КонецЦикла;

			ЭлементСпискаОтрузок["history"] = ИсторияСтатусов;

			ДанныеОтвета["OutboundStatusHistory"].Добавить(ЭлементСпискаОтрузок);

		КонецЦикла;
		
	Иначе
		Для каждого ТекОшибка Из РезультатЗапросаИсторииСтатусов.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат["Ошибки"].Добавить(НоваяОшибка);

		КонецЦикла;

	КонецЕсли;
	
	// формирование Объекта xdto
	Результат["Данные"] = ДанныеОтвета;

КонецПроцедуры

// использовать для api v2
Процедура ПодготовитьИсториюСтатусовЗаявкиНаОтгрузку_v2(ИдентификаторыЗаявкиНаОтгрузку, Результат) Экспорт

	УстановитьПривилегированныйРежим(Истина); 
	
	ЗаявкиНаОтгрузку = Новый Массив;
	
	ИдентификаторЗаявкиВнешний = ИдентификаторыЗаявкиНаОтгрузку["yandexId"];
	ИдентификаторЗаявки = ИдентификаторыЗаявкиНаОтгрузку["partnerId"];
		
	Заявка = РегистрыСведений.дсфИдентификаторыЯндексМаркет.СсылкаНаОбъектПоПубличномуИдентификатору(
																				ИдентификаторЗаявкиВнешний,
																				"ДокументСсылка.дсфЗаявкаНаОтгрузку");
	
	Если Заявка = Неопределено Тогда
		ТекстОшибки = СтрШаблон("Заявка на отгрузку с yandexId %1 не была ранее зарегистрирована.", ИдентификаторЗаявкиВнешний);
			
		Ошибка = дсфОбработкаОшибокЯндексМаркет.ТипДанныхНеНайденОшибка();
		Ошибка.description = ТекстОшибки;

		Результат.Ошибки.Добавить(Ошибка);
		
	Иначе
		ЗаявкиНаОтгрузку.Добавить(Заявка);
			
	КонецЕсли;
	
	РезультатЗапросаИсторииСтатусов = дсфПолучениеСведенийЯндексМаркет.ИсторияСтатусовОтгрузок(ЗаявкиНаОтгрузку);
	
	Если РезультатЗапросаИсторииСтатусов.Успех Тогда
		
		ДанныеПриемки = РезультатЗапросаИсторииСтатусов["ИсторияСтатусовОтгрузок"][0];
		
		ТипОбъекта = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "OutboundStatusHistory");
		ДанныеОтвета = ФабрикаXDTO.Создать(ТипОбъекта);
				
		ТипОбъектаИдентификаторОтгрузки = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "ResourceId");
		ТипОбъектаИсторияСтатусов = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "History");
		
		ТипОбъектаСтатусОтгрузки = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "Status");
		
		//
		ИсторияСтатусов = ФабрикаXDTO.Создать(ТипОбъектаИсторияСтатусов);

		СведенияОбИдентификаторах = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторОтгрузки);
		СведенияОбИдентификаторах["yandexId"] = ДанныеПриемки["ИдентификаторВнешний"];
		СведенияОбИдентификаторах["partnerId"] = ДанныеПриемки["ИдентификаторДокумента"];
		СведенияОбИдентификаторах["fulfillmentId"] = ДанныеПриемки["ИдентификаторСклада"];
		
		ДанныеОтвета["outboundId"] = СведенияОбИдентификаторах;
							
		Для каждого ОписаниеСтатуса Из ДанныеПриемки["Статусы"] Цикл
			
			ЭлементСпискаСтатусов = ФабрикаXDTO.Создать(ТипОбъектаСтатусОтгрузки);
						
			СтатусДокумента = ОписаниеСтатуса["Статус"];
			ЭлементСпискаСтатусов["statusCode"] = дсфСтатусыДокументовЯндексМаркет.КодСтатусаОтгрузкиПоСтатусуДокумента(СтатусДокумента);
			ЭлементСпискаСтатусов["date"] = XMLСтрока(ОписаниеСтатуса["ДатаИзменения"]);
			
			ИсторияСтатусов["outboundStatus"].Добавить(ЭлементСпискаСтатусов);
			
		КонецЦикла;

		ДанныеОтвета["history"] = ИсторияСтатусов;
	
	Иначе
		Для каждого ТекОшибка Из РезультатЗапросаИсторииСтатусов.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат["Ошибки"].Добавить(НоваяОшибка);

		КонецЦикла;

	КонецЕсли;
	
	// формирование Объекта xdto
	Результат["Данные"] = ДанныеОтвета;

КонецПроцедуры

Процедура ПодготовитьСведенияОДеталяхОтгрузки(ИдентификаторыЗаявкиНаОтгрузку, Результат) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторЗаявкиВнешний = ИдентификаторыЗаявкиНаОтгрузку["yandexId"];
	ИдентификаторЗаявки = ИдентификаторыЗаявкиНаОтгрузку["partnerId"];
		
	Заявка = РегистрыСведений.дсфИдентификаторыЯндексМаркет.СсылкаНаОбъектПоПубличномуИдентификатору(
																ИдентификаторЗаявкиВнешний,
																"ДокументСсылка.дсфЗаявкаНаОтгрузку");
	
	Если Заявка = Неопределено Тогда
		ТекстОшибки = СтрШаблон("Заявка на отправку с yandexId %1 не была ранее зарегистрирована.", ИдентификаторЗаявкиВнешний);
		
		Ошибка = дсфОбработкаОшибокЯндексМаркет.ТипДанныхНеНайденОшибка();
		Ошибка.description = ТекстОшибки;

		Результат.Ошибки.Добавить(Ошибка);
		
		Возврат;
		
	КонецЕсли;
	
	РезультатЗапросаДеталейОтгрузки = дсфПолучениеСведенийЯндексМаркет.ДеталиОтгрузкиПоЗаявке(Заявка);
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("outbound", Неопределено);
	ДанныеОтвета.Вставить("registries", Неопределено);
	
	Если РезультатЗапросаДеталейОтгрузки.Успех Тогда
		
		// заявка
		ТипОбъектаЗаявка = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "Outbound");
		ТипОбъектаИдентификаторУниверсальный = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "ResourceId");
		
		ДанныеЗаявки = ФабрикаXDTO.Создать(ТипОбъектаЗаявка);
			
		СведенияОбИдентификаторахЗаявки = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторУниверсальный);
		СведенияОбИдентификаторахЗаявки["yandexId"] = РезультатЗапросаДеталейОтгрузки["ИдентификаторЗаявкиВнешний"];
		СведенияОбИдентификаторахЗаявки["partnerId"] = РезультатЗапросаДеталейОтгрузки["ИдентификаторЗаявки"];
		СведенияОбИдентификаторахЗаявки["fulfillmentId"] = РезультатЗапросаДеталейОтгрузки["ИдентификаторСклада"];

		ДанныеЗаявки["outboundId"] = СведенияОбИдентификаторахЗаявки;
		
		ДанныеЗаявки["outboundType"] = 
				дсфОбщегоНазначенияЯндексМаркет.КодТипаОтправкиПоЗначениюПеречисления(РезультатЗапросаДеталейОтгрузки["ТипОтправки"]);
				
		ДанныеЗаявки["interval"] = РезультатЗапросаДеталейОтгрузки["ПериодОтправкиСтрокой"];
		
		ДанныеОтвета["outbound"] = ДанныеЗаявки;
		
		// реестр приемки - фактический, на момент запроса
		ТипОбъектаСписокРеестров = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "Registries");
		ТипОбъектаРеестр = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "Registry");
		
		СписокРеестров = ФабрикаXDTO.Создать(ТипОбъектаСписокРеестров);
		РеестрПриемки = ФабрикаXDTO.Создать(ТипОбъектаРеестр);
		
		СведенияОбИдентификаторахРеестра = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторУниверсальный);
		СведенияОбИдентификаторахРеестра["yandexId"] = РезультатЗапросаДеталейОтгрузки["ИдентификаторРеестраВнешний"];
		СведенияОбИдентификаторахРеестра["partnerId"] = РезультатЗапросаДеталейОтгрузки["ИдентификаторРеестра"];

		РеестрПриемки["registryId"] = СведенияОбИдентификаторахРеестра;
		РеестрПриемки["outboundId"] = СведенияОбИдентификаторахЗаявки;
					
		РеестрПриемки["registryType"] =
							дсфОбщегоНазначенияЯндексМаркет.КодТипаРеестраПоЗначениюПеречисления(РезультатЗапросаДеталейОтгрузки["ТипРеестра"]);
		РеестрПриемки["date"] = XMLСтрока(РезультатЗапросаДеталейОтгрузки["ДатаРеестра"]);
		РеестрПриемки["comment"] = РезультатЗапросаДеталейОтгрузки["КомментарийКРеестру"];

		// принятые товары
		ТипОбъектаСписокТоваров = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "RegistryItems");
		СписокТоваров = ФабрикаXDTO.Создать(ТипОбъектаСписокТоваров);
		
		ТипОбъектаСтрокаРеестра = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "RegistryItem");
		ТипОбъектаДанныеОТоваре = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "UnitInfo");
		ТипОбъектаСписокСКоличеством = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "UnitCounts");
		ТипОбъектаОписаниеКоличества = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "UnitCount");
		ТипОбъектаДополнительныеИд = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "CompositeId");
		ТипОбъектаИдентификаторыЕдиниц = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "PartialIds");
		ТипОбъектаИдентификаторЕдиницы = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "PartialId");
		
		Для каждого ДанныеОПринятомТоваре Из РезультатЗапросаДеталейОтгрузки["ОтгруженныеТовары"] Цикл
			СтрокаРеестра = ФабрикаXDTO.Создать(ТипОбъектаСтрокаРеестра);
			
			СтрокаРеестра["name"] = ДанныеОПринятомТоваре["Наименование"];
			
			ДанныеОТоваре = ФабрикаXDTO.Создать(ТипОбъектаДанныеОТоваре);
			
			СписокСКоличеством = ФабрикаXDTO.Создать(ТипОбъектаСписокСКоличеством);

			Для каждого ЭлементОписанияКоличества Из ДанныеОПринятомТоваре["СведенияОКоличестве"] Цикл
				ОписаниеКоличества = ФабрикаXDTO.Создать(ТипОбъектаОписаниеКоличества);
				
				Если ТипЗнч(ЭлементОписанияКоличества.Ключ) = Тип("ПеречислениеСсылка.дсфСтатусыЕдиницТовараЯндексМаркет") Тогда
					ОписаниеКоличества["countType"] = XMLСтрока(ЭлементОписанияКоличества.Ключ);
	
				КонецЕсли;
	
				ОписаниеКоличества["quantity"] = XMLСтрока(ЭлементОписанияКоличества.Значение);
				
				СписокСКоличеством["count"].Добавить(ОписаниеКоличества);

			КонецЦикла;
			
			ДанныеОТоваре["counts"] = СписокСКоличеством;
			
			// идентификаторы номенклатуры
			ИдентификаторыНоменклатуры = ФабрикаXDTO.Создать(ТипОбъектаДополнительныеИд);
			СписокИдентификаторовЕдиниц = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторыЕдиниц);
			Для каждого ЭлементОписанияИдентификаторов Из ДанныеОПринятомТоваре["Идентификаторы"] Цикл
				ОписаниеИдентификатора = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторЕдиницы);
				
				Если ТипЗнч(ЭлементОписанияИдентификаторов.Ключ) = Тип("ПеречислениеСсылка.дсфТипыИдентификаторовЯндексМаркет") Тогда
					ОписаниеИдентификатора["idType"] = XMLСтрока(ЭлементОписанияИдентификаторов.Ключ);
	
				КонецЕсли;
	
				ОписаниеИдентификатора["value"] = XMLСтрока(ЭлементОписанияИдентификаторов.Значение);
				
				СписокИдентификаторовЕдиниц["partialId"].Добавить(ОписаниеИдентификатора);

			КонецЦикла;
			
			ИдентификаторыНоменклатуры["partialIds"] = СписокИдентификаторовЕдиниц;

			ДанныеОТоваре["compositeId"] = ИдентификаторыНоменклатуры;
			
			//
			СтрокаРеестра["unitInfo"] = ДанныеОТоваре;
			
			//
			СписокТоваров["item"].Добавить(СтрокаРеестра);
			
		КонецЦикла;
					
		РеестрПриемки["items"] = СписокТоваров;
		
		СписокРеестров["registry"].Добавить(РеестрПриемки);
		ДанныеОтвета["registries"] = СписокРеестров;
		
	Иначе
		Для каждого ТекОшибка Из РезультатЗапросаДеталейОтгрузки.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат["Ошибки"].Добавить(НоваяОшибка);

		КонецЦикла;

	КонецЕсли;
	
	// ответ структурой, т.к. надо возвращать несколько объектов
	Результат["Данные"] = ДанныеОтвета;

КонецПроцедуры

#КонецОбласти

#Область Остатки

Процедура ПодготовитьДанныеПоОстаткамТоваров(Результат,
											КоличествоЗаписей = 20,
											Смещение = 0,
											ОтборПоНоменклатуре = Неопределено) Экспорт
											
	ТаблицаКодовНоменклатуры = Новый ТаблицаЗначений; 
	ТаблицаКодовНоменклатуры.Колонки.Добавить("Артикул", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	
	Если ОтборПоНоменклатуре <> Неопределено Тогда
	
		КодыТоваровСписокXdto = ОтборПоНоменклатуре["unitIds"]["unitId"];
	
		Для каждого КодыТовара Из КодыТоваровСписокXdto Цикл
			НоваяСтрока = ТаблицаКодовНоменклатуры.Добавить();
			НоваяСтрока["Артикул"] = КодыТовара["article"];
			
		КонецЦикла;

	КонецЕсли;
	
	РезультатЗапросаОстатков = дсфПолучениеСведенийЯндексМаркет.СведенияОбОстаткахТоваров(КоличествоЗаписей,
																				Смещение,
																				ТаблицаКодовНоменклатуры);
	
	ТипОбъектаОстатки = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "itemStocksList");
	ДанныеОтвета = ФабрикаXDTO.Создать(ТипОбъектаОстатки);
	
	ТипОбъектаЭлементСписка = ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "itemStock");
	ТипОбъектаИдентификаторТовара= ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "UnitId");
	ТипОбъектаStocks= ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "Stocks");
	ТипОбъектаОстаток= ФабрикаXDTO.Тип("http://www.dsf-yandex-market.kz", "Stock");

	Если РезультатЗапросаОстатков.Успех Тогда
		
		Для каждого Элемент Из РезультатЗапросаОстатков["Товары"] Цикл
			ЭлементСпискаОстатков = ФабрикаXDTO.Создать(ТипОбъектаЭлементСписка);
			
			СведенияОбИдентификаторах = ФабрикаXDTO.Создать(ТипОбъектаИдентификаторТовара);
			СведенияОбИдентификаторах["article"] = Элемент["ИдентификаторТовара"];
			СведенияОбИдентификаторах["vendorId"] = Элемент["ИдентификаторПоставщика"];
			Если ЗначениеЗаполнено(Элемент["ИдентификаторТоварногоПредложения"]) Тогда
				СведенияОбИдентификаторах["id"] = Элемент["ИдентификаторТоварногоПредложения"];

			КонецЕсли;
						
			ЭлементСпискаОстатков["unitId"] = СведенияОбИдентификаторах;
			
			ОстаткиТовара = ФабрикаXDTO.Создать(ТипОбъектаStocks);

			Для каждого ДанныеОбОстатках Из Элемент["Остатки"] Цикл
				Остаток = ФабрикаXDTO.Создать(ТипОбъектаОстаток);
				Остаток["count"] = ДанныеОбОстатках["Количество"];
				Остаток["type"] = ДанныеОбОстатках["ТипСтока"];
				Остаток["update"] = XMLСтрока(ДанныеОбОстатках["Дата"]);
				
				ОстаткиТовара["stock"].Добавить(Остаток);

			КонецЦикла;

			ЭлементСпискаОстатков["stocks"] = ОстаткиТовара; 

			ДанныеОтвета["itemStocks"].Добавить(ЭлементСпискаОстатков);

		КонецЦикла;
	
	Иначе	
		Для каждого ТекОшибка Из РезультатЗапросаОстатков.Ошибки Цикл
			
			НоваяОшибка = дсфОбработкаОшибокЯндексМаркет.НевозможноОбработатьЗапрос();
			НоваяОшибка.description = ТекОшибка;
			
			Результат.Ошибки.Добавить(НоваяОшибка);

		КонецЦикла;
		
	КонецЕсли;
	
	// формирование Объекта xdto
	Результат["Данные"] = ДанныеОтвета;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Код процедур и функций
#КонецОбласти