#Область СлужебныйПрограммныйИнтерфейс

#Область ПроцедурыНачалаОбработкиВходящихЗапросов

Процедура РегистрацияНовыхПозицийНоменклатуры(ОписаниеЗапроса, РезультатОбработкиЗапроса) Экспорт

	ПараметрыПреобразования = Новый Структура;
	ПараметрыПреобразования.Вставить("ПрочитатьВСоответствие", Ложь);

	ДанныеЗапроса = аргмКоннекторHttpA1.JsonВОбъект(ОписаниеЗапроса.Тело, "utf-8", ПараметрыПреобразования);
	
	Ошибки = РезультатОбработкиЗапроса.Ошибки;
	
	Поклажедатель = ОписаниеЗапроса.Клиент;
	
	Если ЗначениеЗаполнено(ОписаниеЗапроса.КлючАпи) Тогда	
		ПрефиксКода = РегистрыСведений.дсфНастройкиДоступаКАпи.ПрефиксКлиентаПоКлючуАпи(ОписаниеЗапроса.КлючАпи);
		
	Иначе
		ПрефиксКода = "";
	
	КонецЕсли;
	
	Результат = РегистрацияНовыхПозицийНоменклатурыРезультат(ДанныеЗапроса, Поклажедатель, ПрефиксКода, Ошибки);
	
	ПоместитьРезультатВыполненияВРезультатОбработкиЗапроса(Результат, РезультатОбработкиЗапроса);

КонецПроцедуры

#КонецОбласти

Процедура ПоместитьРезультатВыполненияВРезультатОбработкиЗапроса(Результат, РезультатОбработкиЗапроса) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		РезультатОбработкиЗапроса.Вставить("Данные", Результат);
		РезультатОбработкиЗапроса.Состояние = Перечисления.аргмСостоянияВходящихЗапросовА1.Завершен;
		
	Иначе
		Ошибки = РезультатОбработкиЗапроса.Ошибки;
		Если Ошибки.Количество() > 0 Тогда
			РезультатОбработкиЗапроса.Данные = Новый Структура("errors", Ошибки);
			
		КонецЕсли;
		
		РезультатОбработкиЗапроса.Состояние = Перечисления.аргмСостоянияВходящихЗапросовА1.ОшибкаВыполнения;
		РезультатОбработкиЗапроса.ОбработанУспешно = Ложь;
		
	КонецЕсли;
	
	РезультатОбработкиЗапроса.ДатаОбработки = ТекущаяДатаСеанса();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РегистрацияНовыхПозицийНоменклатурыРезультат(ДанныеНоменклатуры, Поклажедатель, ПрефиксКода, Ошибки)

	Результат = дсфМенеджерДанныхАпи.РезультатСозданияНовыхПозицийНоменклатуры(ДанныеНоменклатуры,
																				Поклажедатель,
																				ПрефиксКода);
	
	Если Результат.ОбъектыСозданы Тогда
		ДанныеДляОтвета = Новый Структура;
		
		Если Результат.СписокОбъектов.Количество() > 0 Тогда
			
			МассивОписанийНоменклатуры = Новый Массив;
			Для каждого Номенклатура Из Результат.СписокОбъектов Цикл
				СведенияОНоменклатуре = дсфНоменклатураАпи.СведенияОПозицииНоменклатуры(Номенклатура);
				
				МассивОписанийНоменклатуры.Добавить(СведенияОНоменклатуре);
				
			КонецЦикла;
			
			СведенияОНоменклатуре = Новый Структура("goods", МассивОписанийНоменклатуры);
			ДанныеДляОтвета.Вставить("data", СведенияОНоменклатуре);
			
		КонецЕсли;
			
		Если ЗначениеЗаполнено(Результат.Ошибки) Тогда
			ДанныеДляОтвета.Вставить("errors", Результат.Ошибки);
	
		КонецЕсли;
		
		Возврат ДанныеДляОтвета;
		
	Иначе
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Ошибки, Результат.Ошибки); 
		Возврат Неопределено;
		
	КонецЕсли;
	
	//	Результат.Вставить("ОбъектыСозданы", Ложь);
	//Результат.Вставить("СписокОбъектов", Новый Массив);
	//Результат.Вставить("Ошибки", Новый Массив);
	//Результат.Вставить("Сообщения", Новый Массив);
КонецФункции

#КонецОбласти