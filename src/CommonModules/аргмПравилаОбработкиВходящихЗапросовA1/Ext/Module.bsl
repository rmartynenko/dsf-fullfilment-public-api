#Область ПрограммныйИнтерфейс

Функция ПравилаОбработки() Экспорт

	ПравилаОбработки = Новый Соответствие; 
	
	ЗаполнитьДоступныеПравилаОбработки(ПравилаОбработки);
	
	Возврат Новый ФиксированноеСоответствие(ПравилаОбработки);
	
КонецФункции

Функция ИмяПравилаПроверкиРаботоспособности() Экспорт
	Возврат "ТестированиеПроверкаРаботоспособности";
	
КонецФункции

Функция ИмяПравилаНеобрабатываемыйЗапрос() Экспорт
	Возврат "НеобрабатываемыйЗапрос";
	
КонецФункции

Функция ПравилоПроверкиРаботоспособности() Экспорт
	Возврат Новый ФиксированнаяСтруктура(ПравилоПроверкиРаботоспособностиСлужебноеОписание());
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НовоеПравилоОбработкиВходящихЗапросов(Знач НаименованиеПравила) Экспорт
	ПравилоОбработки = Новый Структура;
	
	ПравилоОбработки.Вставить("Наименование", НаименованиеПравила);
	ПравилоОбработки.Вставить("ИмяМетода", "");
	ПравилоОбработки.Вставить("ПроверятьНаличиеТелаЗапроса", Ложь);
	ПравилоОбработки.Вставить("ИменаСвойствСоЗначениямиДата", Новый Массив);
	ПравилоОбработки.Вставить("ПроверятьИдемпотентность", Ложь);
	ПравилоОбработки.Вставить("СрокУстареванияРезультата", 0); // в секундах
	ПравилоОбработки.Вставить("Описание", "");
	ПравилоОбработки.Вставить("ТипЗапроса", "");
	ПравилоОбработки.Вставить("ПроверятьНаличиеКлючаАпи", Ложь);

	Возврат ПравилоОбработки;
	
КонецФункции

Функция ЗаполнитьДоступныеПравилаОбработки(ПравилаОбработки) Экспорт

	// тестовые запросы
	ПравилаОбработки.Вставить(ИмяПравилаПроверкиРаботоспособности(),
								ПравилоПроверкиРаботоспособностиСлужебноеОписание()); 
								
	// необрабатываемый запрос, принимаем, но ответ возращается с ошибкой
	//
	ПравилаОбработки.Вставить(ИмяПравилаНеобрабатываемыйЗапрос(),
								ПравилоДляНеобрабатываемогоЗапросаСлужебноеОписание());
								
	аргмПравилаОбработкиВходящихЗапросовA1Переопределяемый.ЗаполнитьДоступныеПравилаОбработки(ПравилаОбработки);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПравилоПроверкиРаботоспособностиСлужебноеОписание()
	ИмяПравила = ИмяПравилаПроверкиРаботоспособности();
	
	ПравилоОбработки = НовоеПравилоОбработкиВходящихЗапросов(ИмяПравила);
	
	ПравилоОбработки["ИмяМетода"] = "аргмОбработкаВходящихЗапросовА1.ПроверкаРаботоспособности";
	ПравилоОбработки["ПроверятьНаличиеТелаЗапроса"] = Истина;
	
	ИменаСвойствСоЗначениямиДата = Новый Массив; 
	ИменаСвойствСоЗначениямиДата.Добавить("date");
	ПравилоОбработки["ИменаСвойствСоЗначениямиДата"] = ИменаСвойствСоЗначениямиДата;
	
	ПравилоОбработки["ПроверятьИдемпотентность"] = Истина;
	ПравилоОбработки["СрокУстареванияРезультата"] = 10;
	
	Возврат ПравилоОбработки;
	
КонецФункции

Функция ПравилоДляНеобрабатываемогоЗапросаСлужебноеОписание()
	ИмяПравила = ИмяПравилаНеобрабатываемыйЗапрос();
	
	ПравилоОбработки = НовоеПравилоОбработкиВходящихЗапросов(ИмяПравила);
	
	ПравилоОбработки["ИмяМетода"] = "аргмОбработкаВходящихЗапросовА1.ОтветитьНаНеобрабатываемыйЗапрос";
	
	Возврат ПравилоОбработки;
	
КонецФункции

#КонецОбласти