#Область ПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// проверяет наличие в информационной базе объекта с указанным идентификатором
// параметры
//      ИдентификаторОбъекта - Строка или УникальныйИдентификатор
// 		ПолноеИмяОбъекта     - Строка - "Справочник.Номенклатура"
//
// Возвращает Ссылку на объект или Неопределено
//
Функция ПроверитьНаличиеОбъектаСУказаннымИдентификатором(ИдентификаторОбъекта,
														ПолноеИмяОбъекта,
														Ошибки = Неопределено) Экспорт
	
	Если Ошибки = Неопределено Тогда
		Ошибки = Новый Массив;
		
	КонецЕсли;
	
	ОбъектСсылка = Неопределено;
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяОбъекта);

	Если ТипЗнч(ИдентификаторОбъекта) = Тип("УникальныйИдентификатор") Тогда
		ОбъектСсылка = Менеджер.ПолучитьСсылку(ИдентификаторОбъекта);
		
	ИначеЕсли ТипЗнч(ИдентификаторОбъекта) = Тип("Строка") Тогда
		
		Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдентификаторОбъекта) Тогда
			ОбъектСсылка = Менеджер.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдентификаторОбъекта));
			
		Иначе
			ШаблонТекстаОшибки = НСтр("ru='Значение [%1] не является корректным идентификатором объекта.'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаОшибки, ИдентификаторОбъекта);
			Ошибки.Добавить(ТекстОшибки);
			
			Возврат Неопределено;
			
		КонецЕсли;
		
	Иначе
		ШаблонТекстаОшибки = НСтр("ru='Значение [%1] не является идентификатором объекта.'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаОшибки, ИдентификаторОбъекта);
		Ошибки.Добавить(ТекстОшибки);
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Если НЕ ОбщегоНазначения.СсылкаСуществует(ОбъектСсылка) ИЛИ ОбъектСсылка.ПолучитьОбъект() = Неопределено Тогда
		// объект не обнаружен
		ШаблонТекстаОшибки = НСтр("ru='Объект %1 с id [%2] не зарегистрирован.'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаОшибки,
																				ПолноеИмяОбъекта,
																				ИдентификаторОбъекта);
		Ошибки.Добавить(ТекстОшибки);
			
		Возврат Неопределено;
		
	КонецЕсли;
		
	Возврат ОбъектСсылка;
	
КонецФункции 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Код процедур и функций
#КонецОбласти