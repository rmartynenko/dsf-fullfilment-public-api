#Область ПрограммныйИнтерфейс

Функция ОписаниеКодаСтатусаПриемки(КодСтатуса) Экспорт
	ДопустимыеКодыСтатусов = дсфСтатусыДокументовЯндексМаркетПовтИсп.ДопустимыеКодыСтатусовПриемки();
	
	ОписаниеКодаСтатусаПриемки = Неопределено;
	Для Каждого ОписаниеКода Из ДопустимыеКодыСтатусов Цикл
		Если ОписаниеКода.Код = КодСтатуса Тогда
			ОписаниеКодаСтатусаПриемки = ОписаниеКода.Описание;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если ОписаниеКодаСтатусаПриемки = Неопределено Тогда
		Возврат СтрШаблон("Для статуса приемки %1 описание не определено", КодСтатуса);	
		
	Иначе
		Возврат ОписаниеКодаСтатусаПриемки;
		
	КонецЕсли;
	
КонецФункции

// Функция - Код статуса приемки по статусу документа
//
// Параметры:
//  Статус	 - СправочникСсылка.СтатусыДокументов - Статус документа приемки
// 
// Возвращаемое значение:
//  КодСтатуса - Число
//
Функция КодСтатусаПриемкиПоСтатусуДокумента(Статус) Экспорт

	ДопустимыеКодыСтатусов = дсфСтатусыДокументовЯндексМаркетПовтИсп.ДопустимыеКодыСтатусовПриемки();
	
	КодСтатусаПриемки = Неопределено;
	Для Каждого ОписаниеКода Из ДопустимыеКодыСтатусов Цикл
		Если ОписаниеКода.Статус = Статус Тогда
			КодСтатусаПриемки = ОписаниеКода.Код;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если КодСтатусаПриемки = Неопределено Тогда
		Возврат -1;
		
	Иначе
		Возврат КодСтатусаПриемки;
		
	КонецЕсли;
	

КонецФункции

// Функция - Код статуса отгрузки по статусу документа
//
// Параметры:
//  Статус	 - СправочникСсылка.СтатусыДокументов - Статус документа приемки
// 
// Возвращаемое значение:
//  КодСтатуса - Число
//
Функция КодСтатусаОтгрузкиПоСтатусуДокумента(Статус, ОтгруженЧастично = Ложь) Экспорт

	ДопустимыеКодыСтатусов = дсфСтатусыДокументовЯндексМаркетПовтИсп.ДопустимыеКодыСтатусовОтгрузки();
	
	КодСтатусаОтгрузки = Неопределено;
	Для Каждого ОписаниеКода Из ДопустимыеКодыСтатусов Цикл
		Если ОписаниеКода.Статус = Статус Тогда
			КодСтатусаОтгрузки = ОписаниеКода.Код;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если КодСтатусаОтгрузки = Неопределено Тогда
		// 2 - при определении статуса произошла ошибка. См. описание формата ответа, полученное от заказчика
		Возврат 2;
		
	Иначе
		Возврат КодСтатусаОтгрузки;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
Процедура ОжидаемоеПоступлениеПриЗаписи(Источник, Отказ) Экспорт
	
	Если Не Отказ Тогда 
		МассивДокументов = Новый Массив;
		МассивДокументов.Добавить(Источник.Ссылка);
		
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(МассивДокументов);

		ФоновыеПроцессы.ВыполнитьФоновоеЗадание("дсфСтатусыДокументовЯндексМаркет.СвязатьОжидаемыеПоступленияСЗаявкамиНаПриемку",
												МассивПараметров,,,
												НСтр("ru = 'Связать ожидаемые поступления с заявками на приемку'"));

	КонецЕсли;

КонецПроцедуры

Процедура ЗаказНаОтгрузкуПриЗаписи(Источник, Отказ) Экспорт
	
	Если Не Отказ Тогда 
		МассивДокументов = Новый Массив;
		МассивДокументов.Добавить(Источник.Ссылка);
		
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(МассивДокументов);

		ФоновыеПроцессы.ВыполнитьФоновоеЗадание("дсфСтатусыДокументовЯндексМаркет.СвязатьЗаказыНаОтгрузкуСЗаявками",
												МассивПараметров,,,
												НСтр("ru = 'Связать заказы на отгрузку с заявками'"));

	КонецЕсли;

КонецПроцедуры

Процедура СвязатьОжидаемыеПоступленияСЗаявкамиНаПриемку(ОжидаемыеПоступления) Экспорт
	РегистрыСведений.дсфЗаявкаНаПриемкуСвязанныеОбъектыЯМаркет.СвязатьОжидаемыеПоступленияСЗаявкамиНаПриемку(ОжидаемыеПоступления);
	
КонецПроцедуры

Процедура СвязатьЗаказыНаОтгрузкуСЗаявками(ЗаказыНаОтгрузку) Экспорт
	РегистрыСведений.дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет.СвязатьЗаказыНаОтгрузкуСЗаявками(ЗаказыНаОтгрузку);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Код процедур и функций
#КонецОбласти