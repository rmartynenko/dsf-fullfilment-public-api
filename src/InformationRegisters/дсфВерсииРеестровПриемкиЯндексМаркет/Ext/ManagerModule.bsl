// Процедура добавляет запись в регистр по переданным значениям структуры.
Процедура ДобавитьЗапись(СтруктураЗаписи, Загрузка = Ложь) Экспорт
	Если СтруктураЗаписи.Свойство("ДатаИзменения") = Ложь Тогда
		СтруктураЗаписи.Вставить("ДатаИзменения", ТекущаяДатаСеанса());
		
	КонецЕсли;
	
	дсфОбменДаннымиСлужебный.ДобавитьЗаписьВРегистрСведений(СтруктураЗаписи, "дсфВерсииРеестровПриемкиЯндексМаркет", Загрузка);
	
КонецПроцедуры

// Процедура удаляет набор записей в регистре по переданным значениям структуры.
Процедура УдалитьЗапись(СтруктураЗаписи, Загрузка = Ложь) Экспорт
	
	дсфОбменДаннымиСлужебный.УдалитьНаборЗаписейВРегистреСведений(СтруктураЗаписи,
																	"дсфВерсииРеестровПриемкиЯндексМаркет",
																	Загрузка);
	
КонецПроцедуры

// Процедура обновляет набор записей в регистре по переданным значениям структуры.
Процедура ОбновитьЗапись(СтруктураЗаписи) Экспорт
	
	Если СтруктураЗаписи.Свойство("ДатаИзменения") = Ложь Тогда
		СтруктураЗаписи.Вставить("ДатаИзменения", ТекущаяДатаСеанса());
		
	КонецЕсли;

	дсфОбменДаннымиСлужебный.ОбновитьЗаписьВРегистреСведений(СтруктураЗаписи, "дсфВерсииРеестровПриемкиЯндексМаркет");
	
КонецПроцедуры

// Возвращает признак наличия записи в регистре по переданному отбору.
//
// Параметры:
//   ЗаявкаНаПриемку - ДокументСсылка - ЗаявкаНаПриемку
//   ИдентификаторРеестра - Строка - Внешний идентификатор реестра (yandexId):
//
// Возвращаемое значение:
//   Булево - Истина, если в регистре содержатся записи для заданного отбора.
//
Функция ЗаписьЕстьВРегистре(ЗаявкаНаПриемку, ИдентификаторРеестра) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЕстьЗапись
	|ИЗ
	|	РегистрСведений.дсфВерсииРеестровПриемкиЯндексМаркет КАК ВерсииРеестровПриемкиЯндексМаркет
	|ГДЕ
	|	ВерсииРеестровПриемкиЯндексМаркет.ЗаявкаНаПриемку = &ЗаявкаНаПриемку
	|	И ВерсииРеестровПриемкиЯндексМаркет.ИдентификаторРеестра = &ИдентификаторРеестра");

	Запрос.УстановитьПараметр("ЗаявкаНаПриемку", ЗаявкаНаПриемку);
	Запрос.УстановитьПараметр("ИдентификаторРеестра", ИдентификаторРеестра);

	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

