#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
// Функция - План поступления по заявке на приемку
//
// Параметры:
//  ЗаявкаНаПриемку	 - ДокументСсылка.dsf_ЗаявкаНаПриемку	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция ПланПоступленияПоЗаявкеНаПриемку(ЗаявкаНаПриемку) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет.ПланПоступления КАК ПланПоступления
	|ИЗ
	|	РегистрСведений.дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет КАК дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет
	|ГДЕ
	|	дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет.ЗаявкаНаПриемку = &ЗаявкаНаПриемку");

	Запрос.УстановитьПараметр("ЗаявкаНаПриемку", ЗаявкаНаПриемку);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.ПланПоступления;
		
	Иначе
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий
// Код процедур и функций
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьНовуюЗапись(ЗаявкаНаПриемку) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет");
		ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПриемку", ЗаявкаНаПриемку);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = РегистрыСведений.дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет.СоздатьМенеджерЗаписи();
		Запись.ЗаявкаНаПриемку = ЗаявкаНаПриемку;
		Запись.Прочитать();
		
		Если Не ЗначениеЗаполнено(Запись.ДатаИзменения) Тогда
			Запись.ЗаявкаНаПриемку = ЗаявкаНаПриемку;

		КонецЕсли;
		
		Запись.ДатаИзменения = ТекущаяДатаСеанса();
		
		Запись.Записать();
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При внесении записи в регистр сведений dsf_СвязанныеОбъектыЗаявкаНаПриемку произошла ошибка:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(дсфВходящиеЗапросыЯндексМаркет.ИмяСобытияЖурналаРегистрации(),
								УровеньЖурналаРегистрации.Ошибка,
								Метаданные.HTTPСервисы.дсфFulfillmentYandexMarket,,
								ТекстИсключения);
								
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
КонецПроцедуры

Процедура СвязатьПланПоступленияСЗаявкойНаПриемку(ПланПоступления, ЗаявкаНаПриемку) Экспорт
	
	СтруктураЗаписи = Новый Структура;
	
	СтруктураЗаписи.Вставить("ЗаявкаНаПриемку", ЗаявкаНаПриемку);
	СтруктураЗаписи.Вставить("ПланПоступления", ПланПоступления);
	СтруктураЗаписи.Вставить("ДатаИзменения", ТекущаяДатаСеанса());
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет");
		ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПриемку", ЗаявкаНаПриемку);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
				
		дсфОбменДаннымиСлужебный.ОбновитьЗаписьВРегистреСведений(СтруктураЗаписи, "дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет");
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При записи данных возникли ошибки:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
		
КонецПроцедуры

Процедура СвязатьОжидаемоеПоступлениеСЗаявкойНаПриемку(ОжидаемоеПоступление, ЗаявкаНаПриемку) Экспорт
	
	СтруктураЗаписи = Новый Структура;
	
	СтруктураЗаписи.Вставить("ЗаявкаНаПриемку", ЗаявкаНаПриемку);
	СтруктураЗаписи.Вставить("ОжидаемоеПоступление", ОжидаемоеПоступление);
	СтруктураЗаписи.Вставить("ДатаИзменения", ТекущаяДатаСеанса());
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет");
		ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПриемку", ЗаявкаНаПриемку);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
				
		дсфОбменДаннымиСлужебный.ОбновитьЗаписьВРегистреСведений(СтруктураЗаписи, "дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет");
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При записи данных возникли ошибки:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
		
КонецПроцедуры

Процедура СвязатьОжидаемыеПоступленияСЗаявкамиНаПриемку(ОжидаемыеПоступления) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОжидаемоеПоступление.Ссылка КАК ОжидаемоеПоступлениеСсылка
	               |ПОМЕСТИТЬ ВТ_ОжидаемыеПоступления
	               |ИЗ
	               |	Документ.ОжидаемоеПоступление КАК ОжидаемоеПоступление
	               |ГДЕ
	               |	ОжидаемоеПоступление.Ссылка В(&МассивДокументов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПланПоступления.Ссылка КАК ПланПоступления,
	               |	ВТ_ОжидаемыеПоступления.ОжидаемоеПоступлениеСсылка КАК ОжидаемоеПоступление
	               |ПОМЕСТИТЬ ВТ_ПлановыеДокументыПоступления
	               |ИЗ
	               |	Документ.ПланПоступления КАК ПланПоступления
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОжидаемыеПоступления КАК ВТ_ОжидаемыеПоступления
	               |		ПО ПланПоступления.ОжидаемоеПоступление = ВТ_ОжидаемыеПоступления.ОжидаемоеПоступлениеСсылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет.ЗаявкаНаПриемку КАК ЗаявкаНаПриемку,
	               |	ВТ_ПлановыеДокументыПоступления.ПланПоступления КАК ПланПоступления,
	               |	ВТ_ПлановыеДокументыПоступления.ОжидаемоеПоступление КАК ОжидаемоеПоступление
	               |ИЗ
	               |	ВТ_ПлановыеДокументыПоступления КАК ВТ_ПлановыеДокументыПоступления
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет КАК дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет
	               |		ПО ВТ_ПлановыеДокументыПоступления.ПланПоступления = дсфСвязанныеОбъектыЗаявкаНаПриемкуЯндексМаркет.ПланПоступления";
	
	Запрос.УстановитьПараметр("МассивДокументов", ОжидаемыеПоступления);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СвязатьОжидаемоеПоступлениеСЗаявкойНаПриемку(Выборка.ОжидаемоеПоступление, Выборка.ЗаявкаНаПриемку);
		
	КонецЦикла;
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Код процедур и функций
#КонецОбласти
	
#КонецЕсли
	
