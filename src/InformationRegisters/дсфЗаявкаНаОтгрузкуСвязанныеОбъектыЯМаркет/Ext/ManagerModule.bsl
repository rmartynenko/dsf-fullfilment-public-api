#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПланОтгрузкиПоЗаявкеНаОтгрузку(Заявка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет.ПланОтгрузки КАК ПланОтгрузки
	|ИЗ
	|	РегистрСведений.дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет КАК дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет
	|ГДЕ
	|	дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет.ЗаявкаНаОтгрузку = &ЗаявкаНаОтгрузку");

	Запрос.УстановитьПараметр("ЗаявкаНаОтгрузку", Заявка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.ПланОтгрузки;
		
	Иначе
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий
// Код процедур и функций
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьНовуюЗапись(ЗаявкаНаОтгрузку) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет");
		ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаОтгрузку", ЗаявкаНаОтгрузку);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = РегистрыСведений.дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет.СоздатьМенеджерЗаписи();
		Запись.ЗаявкаНаОтгрузку = ЗаявкаНаОтгрузку;
		Запись.Прочитать();
		
		Если Не ЗначениеЗаполнено(Запись.ДатаИзменения) Тогда
			Запись.ЗаявкаНаОтгрузку = ЗаявкаНаОтгрузку;

		КонецЕсли;
		
		Запись.ДатаИзменения = ТекущаяДатаСеанса();
		
		Запись.Записать();
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При внесении записи в регистр сведений дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯндексМаркет произошла ошибка:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(дсфВходящиеЗапросыЯндексМаркет.ИмяСобытияЖурналаРегистрации(),
								УровеньЖурналаРегистрации.Ошибка,
								Метаданные.HTTPСервисы.дсфFulfillmentYandexMarket,,
								ТекстИсключения);
								
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
КонецПроцедуры

Процедура СвязатьПланОтгрузкиСЗаявкой(ПланОтгрузки, ЗаявкаНаОтгрузку) Экспорт
	
	СтруктураЗаписи = Новый Структура;
	
	СтруктураЗаписи.Вставить("ЗаявкаНаОтгрузку", ЗаявкаНаОтгрузку);
	СтруктураЗаписи.Вставить("ПланОтгрузки", ПланОтгрузки);
	СтруктураЗаписи.Вставить("ДатаИзменения", ТекущаяДатаСеанса());
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет");
		ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаОтгрузку", ЗаявкаНаОтгрузку);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
				
		дсфОбменДаннымиСлужебный.ОбновитьЗаписьВРегистреСведений(СтруктураЗаписи, "дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет");
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При записи данных возникли ошибки:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
		
КонецПроцедуры

Процедура СвязатьЗаказНаОтгрузкуСЗаявкой(ЗаказНаОтгрузку, ЗаявкаНаОтгрузку) Экспорт
	
	СтруктураЗаписи = Новый Структура;
	
	СтруктураЗаписи.Вставить("ЗаявкаНаОтгрузку", ЗаявкаНаОтгрузку);
	СтруктураЗаписи.Вставить("ЗаказНаОтгрузку", ЗаказНаОтгрузку);
	СтруктураЗаписи.Вставить("ДатаИзменения", ТекущаяДатаСеанса());
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет");
		ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаОтгрузку", ЗаявкаНаОтгрузку);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
				
		дсфОбменДаннымиСлужебный.ОбновитьЗаписьВРегистреСведений(СтруктураЗаписи, "дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет");
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При записи данных возникли ошибки:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
		
КонецПроцедуры

Процедура СвязатьЗаказыНаОтгрузкуСЗаявками(ЗаказыНаОтгрузку) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказНаОтгрузку.Ссылка КАК ЗаказНаОтгрузкуСсылка
	               |ПОМЕСТИТЬ ВТ_ЗаказыНаОтгрузку
	               |ИЗ
	               |	Документ.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку
	               |ГДЕ
	               |	ЗаказНаОтгрузку.Ссылка В(&МассивДокументов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПланОтгрузки.Ссылка КАК ПланОтгрузки,
	               |	ВТ_ЗаказыНаОтгрузку.ЗаказНаОтгрузкуСсылка КАК ЗаказНаОтгрузку
	               |ПОМЕСТИТЬ ВТ_ПлановыеДокументыОтгрузки
	               |ИЗ
	               |	Документ.ПланОтгрузки КАК ПланОтгрузки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗаказыНаОтгрузку КАК ВТ_ЗаказыНаОтгрузку
	               |		ПО ПланОтгрузки.ЗаказНаОтгрузку = ВТ_ЗаказыНаОтгрузку.ЗаказНаОтгрузкуСсылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет.ЗаявкаНаОтгрузку КАК ЗаявкаНаОтгрузку,
	               |	ВТ_ПлановыеДокументыОтгрузки.ПланОтгрузки КАК ПланОтгрузки,
	               |	ВТ_ПлановыеДокументыОтгрузки.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку
	               |ИЗ
	               |	ВТ_ПлановыеДокументыОтгрузки КАК ВТ_ПлановыеДокументыОтгрузки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет КАК дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет
	               |		ПО ВТ_ПлановыеДокументыОтгрузки.ПланОтгрузки = дсфЗаявкаНаОтгрузкуСвязанныеОбъектыЯМаркет.ПланОтгрузки";
	
	Запрос.УстановитьПараметр("МассивДокументов", ЗаказыНаОтгрузку);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СвязатьЗаказНаОтгрузкуСЗаявкой(Выборка.ЗаказНаОтгрузку, Выборка.ЗаявкаНаОтгрузку);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Код процедур и функций
#КонецОбласти
	
#КонецЕсли
	
